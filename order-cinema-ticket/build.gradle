plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.6'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'maven-publish'
    id 'org.flywaydb.flyway' version '9.17.0'
    id 'com.palantir.git-version' version '0.15.0'
}

group = 'com.agri'
version gitVersion()

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    lombokVersion = "1.18.26"
    lombokMapstructBindingVersion = "0.2.0"
    restAssuredVersion = "5.3.0"
    testContainerVersion = "1.18.0"
    mapstructVersion = "1.5.5.Final"
}

jar {
    enabled = false
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    runtimeOnly 'org.postgresql:postgresql:42.6.0'
    implementation 'commons-io:commons-io:2.12.0'

    /* lombok */
    implementation "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    implementation "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"
    annotationProcessor "org.projectlombok:lombok-mapstruct-binding:${lombokMapstructBindingVersion}"
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    /* rest assured */
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test:6.0.3'
    testImplementation "io.rest-assured:spring-mock-mvc:${restAssuredVersion}"
    testImplementation "io.rest-assured:json-path:${restAssuredVersion}"
    testImplementation "io.rest-assured:xml-path:${restAssuredVersion}"

    /* test container */
    testImplementation "org.testcontainers:testcontainers:${testContainerVersion}"
    testImplementation "org.testcontainers:junit-jupiter:${testContainerVersion}"

    /* mapstruct */
    implementation "org.mapstruct:mapstruct:${mapstructVersion}"
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
}

bootRun {
    def profile = project.findProperty('profile') ?: 'prod'
    args += "--spring.profiles.active=$profile"

    // Detail configuration guide: https://docs.gradle.org/current/dsl/org.gradle.api.tasks.JavaExec.html
    debugOptions {
        port = 8717
        suspend = false
    }
}


tasks.named('test') {
    useJUnitPlatform()
}
